#Область ПрограммныйИнтерфейс

#Область ИсходящаяОчередь

// Функция - Получить структуру объекта класса
// Функция получает для ссылки класса интеграции структуру данных для формирования тела сообщения
// Параметры:
//  ссКласс		-	ссылка Справочник.сшпКлассыИнтеграции - Ссылка на элемент справочника классы интеграции
// 
// Возвращаемое значение:
//  Структура данных для формирования тела сообщения на основании определения класса интеграции (см.реквизит Определение).
//
Функция ПолучитьСтруктуруОбъектаКласса(ссКласс) Экспорт
	
	стрСтруктураОбъектов = ссКласс.Определение;
	
	Если ПустаяСтрока(стрСтруктураОбъектов) Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	стДанные = сшпФункцииРаботыJSON.ПростоеЧтениеJSON(стрСтруктураОбъектов);
	
	Возврат стДанные;
	
КонецФункции

// Функция - Получить массив классов для объекта
// Функция получает для объекта / ссылки массив классов интеграции
// Параметры:
//  обДанные	  -	объект / ссылка - Объект / ссылка на справочник / документ 1С
// 
// Возвращаемое значение:
//  Массив ссылок на классы интеграции для переданного объекта / ссылки.
//
Функция ПолучитьМассивКлассовДляОбъекта(обДанные) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	сшпКлассыИнтеграции.Ссылка КАК Класс
	               |ИЗ
	               |	Справочник.сшпКлассыИнтеграции КАК сшпКлассыИнтеграции
	               |ГДЕ
	               |	сшпКлассыИнтеграции.Наименование = &Наименование
	               |	И сшпКлассыИнтеграции.ТипИнтеграции = &ТипИнтеграции
	               |	И сшпКлассыИнтеграции.Отключен = &Отключен";
	
	Запрос.УстановитьПараметр("Наименование", обДанные.Метаданные().ПолноеИмя());
	Запрос.УстановитьПараметр("ТипИнтеграции", Перечисления.сшпТипыИнтеграции.Исходящая);
	Запрос.УстановитьПараметр("Отключен", Ложь);
	
	спКлассы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Класс");
	
	Возврат спКлассы;
	
КонецФункции

// Функция - Получить структуру данных для формирования сообщения в 1С:Шина
// Функция получает структуру данных для формирования сообщения в системной очереди 1С:Предприятие.
// Параметры:
//  ссКласс		-	ссылка Справочник.сшпКлассыИнтеграции - Ссылка на элемент справочника классы интеграции
// 
// Возвращаемое значение:
//  Фиксированная структура данных для формирования сообщения в системной очереди 1С:Предприятие.
//
Функция ПолучитьСтруктуруДанныхДляФормированияСообщенияВСШП(ссКласс) Экспорт
	
	стДанные = Новый Структура;
	стДанные.Вставить("ИмяСервисаИнтеграции", СокрЛП(ссКласс.СервисИнтеграции.Наименование));
	стДанные.Вставить("ИмяКанала", СокрЛП(ссКласс.Канал.Наименование));
	стДанные.Вставить("КодОтправителя", сшпОбщегоНазначения.ПолучитьКодТекущейИнфосистемы());
	стДанные.Вставить("КодПолучателя", СокрЛП(ссКласс.Инфосистема.Код));
	стДанные.Вставить("ТипЗапроса", СтрЗаменить(ссКласс.ТипЗапроса, " ", ""));
	
	фстДанные = Новый ФиксированнаяСтруктура(стДанные);
	
	Возврат фстДанные;
	
КонецФункции

#КонецОбласти

#Область ВходящаяОчередь

// Функция - Получить массив классов по имени
// Функция получает по имени класса входящего сообщения массив классов интеграции
// Параметры:
//  стрИмяКласса	  -	строка - Имя класса входящего сообщения
// 
// Возвращаемое значение:
//  Массив ссылок на классы интеграции для переданного имени класса.
//
Функция ПолучитьМассивКлассовПоИмени(стрИмяКласса) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	сшпКлассыИнтеграции.Ссылка КАК Класс
	               |ИЗ
	               |	Справочник.сшпКлассыИнтеграции КАК сшпКлассыИнтеграции
	               |ГДЕ
	               |	сшпКлассыИнтеграции.Наименование = &Наименование
	               |	И сшпКлассыИнтеграции.ТипИнтеграции = &ТипИнтеграции
	               |	И сшпКлассыИнтеграции.Отключен = &Отключен";
	
	Запрос.УстановитьПараметр("Наименование", стрИмяКласса);
	Запрос.УстановитьПараметр("ТипИнтеграции", Перечисления.сшпТипыИнтеграции.Входящая);
	Запрос.УстановитьПараметр("Отключен", Ложь);
	
	спКлассы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Класс");
	
	Возврат спКлассы;
	
КонецФункции

#КонецОбласти

#КонецОбласти